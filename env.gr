import Process from "sys/process"
import Map from "map"
import Array from "array"
import String from "string"
import Option from "option"

// Split an environment variable at the first equals sign.
// @param item: An environment variable pair, separated by an equals sign (=).
// @return (String, String) A tuple key/value pair.
let splitEnvVar = (item) => {
    let offsetOpt = String.indexOf("=", item)

    // For now, fail if the env var is malformed.
    let offset = Option.unwrap(offsetOpt)

    let key = String.slice(0, offset, item)
    let val = String.slice(offset+1, String.length(item), item)
    (key, val)
}

// Get the environment variables as a Map<String, String>
//
// @return Map<String, String> A map of all environment variables.
export let envMap = () =>  {
    let parsed = Map.make()
    let env = Process.env()
    let pairs = Array.map(splitEnvVar,env)

    Array.forEach((item) => {
        let (k, v) = item
        Map.set(k, v, parsed)
    }, pairs)
    parsed
}


